buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "4.0.4"
    id "java"
}
apply plugin: "net.minecraftforge.gradle.forge"

repositories {
    jcenter()
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    shade "org.apache.commons:commons-lang3:3.12.0"
    shade "org.apache.httpcomponents:httpasyncclient:4.1.5"
}

shadowJar {
    classifier = ""
    configurations = [project.configurations.shade]
    exclude "*/*.txt"
    minimize {
        exclude(dependency("commons-logging:commons-logging:.*"))
    }
    relocate("org", "shadow.org")
}
reobf {
    shadowJar {}
}
shadowJar.finalizedBy('reobfShadowJar')

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}
version = "${mod_version}"
group = "${group}"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    replace "@VERSION@", project.version
    replaceIn "HighpixelSpeed.java"
    mappings = "stable_22"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "mod_version", project.version
    inputs.property "mc_version", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "mod_id":"${mod_id}",
                "mod_name":"${mod_name}",
                "mod_version":"${mod_version}",
                "mod_description":"${mod_description}",
                "mc_version":"${mc_version}"
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}
//Intellij IDEA fix. Uncomment for building with IDEA

//sourceSets { main { output.resourcesDir = output.classesDir } }
